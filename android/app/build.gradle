def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader -> localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"
apply plugin: 'com.google.gms.google-services'


def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}


if (project.hasProperty("local.signing")) {
    apply from: getSigningFile()
}

def getSigningFile() {
    return project.property("local.signing");
}

def apkNamePrefix = "mCollect"
def sprintNumber = "0"

android {
    compileSdkVersion 33

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.mcollect.origa.ai"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 31
        versionName "23.10.01"
        multiDexEnabled true
        ndk {
            abiFilters "armeabi", "x86", "armeabi-v7a", "x86_64", "mips",
                    "mips64", "arm64-v8a"
        }
        android.applicationVariants.all { variant ->
            variant.outputs.all {
                def displayVersionName = versionName + "." + sprintNumber + "." + flutterVersionCode != null ? flutterVersionCode : 1
                def date = getDate()
                def modifiedApkName = apkNamePrefix + "-" + displayVersionName + "-" + variant.flavorName + "-" + variant.buildType.name + "-" + date + ".apk"
                outputFileName = modifiedApkName
                def fullVersionName = versionName + "." + sprintNumber
                buildConfigField("String", "displayVersionName", "\"" + displayVersionName + "\"")
                buildConfigField("String", "fullVersionName", "\"" + fullVersionName + "\"")
            }
        }
    }
    buildTypes {
        def CHARACTER = "Character"
        def XPOSELEVEL = "exposeLevel"
        release {
            buildConfigField(CHARACTER, XPOSELEVEL, "null")
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
//            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
//            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        uat {
            dimension "default"
            resValue "string", "app_name", "M2P Collect UAT"
        }
        dev {
            dimension "default"
            resValue "string", "app_name", "M2P Collect Dev"
        }
        prod {
            dimension "default"
            resValue "string", "app_name", "M2P Collect"
        }
    }
}

flutter {
    source '../..'
}


def getVersionName(majorNumber, minorNumber) {
    def computedVersionName = majorNumber + "." + minorNumber
    println "Version name is set to $computedVersionName"
    return computedVersionName
}

static def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}

dependencies {
    //noinspection GradleDependency
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.scottyab:rootbeer-lib:0.1.0'
}